<!DOCTYPE html><html><body><script src="https://georgeweigt.github.io/eigenmath.js"></script><textarea id="stdin" rows="24" cols="80" style="font-family:courier;font-size:1em">
"Problem 11.7"

ca0 = 1
cb0 = 0

dca = -i/hbar Hab exp(-i omega0 t) cb
dcb = -i/hbar Hba exp(i omega0 t) ca

dca1 = eval(dca, cb, cb0)
dcb1 = eval(dcb, ca, ca0)

ca1 = ca0 + defint(dca1, t, 0, t)
cb1 = cb0 + defint(dcb1, t, 0, t)

dca2 = eval(dca, cb, cb1)
dcb2 = eval(dcb, ca, ca1)

ca2 = ca0 + defint(dca2, t, 0, t)
cb2 = cb0 + defint(dcb2, t, 0, t)

check(dca1 == 0)
check(dcb1 == -i / hbar Hba exp(i omega0 t))

check(ca1 == 1)
check(cb1 == Hba / (hbar omega0) (1 - exp(i omega0 t)))

check(dca2 == i Hab Hba / (hbar^2 omega0) (1 - exp(-i omega0 t)))
check(dcb2 == -i Hba / hbar exp(i omega0 t))

check(ca2 == 1 + Hab Hba / (hbar^2 omega0^2) (i omega0 t + exp(-i omega0 t) - 1))
check(cb2 == Hba / (hbar omega0) (1 - exp(i omega0 t)))

"ok"
</textarea> <button onclick="run()">Run</button><br><div id="stdout"></div></body></html>
